buildscript {
	repositories {
	    mavenLocal()
  		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
	}
}

subprojects {
	group = 'io.opencaesar.owl'
	version = '0.9.0'
	
	ext.versions = [
		owl: '5.1.15',
		openllet: '2.6.5',
		jena: '3.16.0',
		jgrapht: '1.1.0',
		xmlcatalog: '1.0.4',
		xmlresolver: '1.2',
		jcommander: '1.72',
		log4j: '1.2.17',
		slf4j: '1.7.30',
		junit: '4.12'
	]

	repositories {
    	mavenLocal()
  		mavenCentral()
		jcenter()
	}
	
	apply plugin: 'java-library'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'com.jfrog.bintray'
	
	apply from: "${rootDir}/gradle/source-layout.gradle"
	apply from: "${rootDir}/gradle/maven-deployment.gradle"
		
	sourceCompatibility = '11'
	targetCompatibility = '11'

	dependencies {
	 	implementation "com.beust:jcommander:${versions.jcommander}"
		implementation "log4j:log4j:${versions.log4j}"
		implementation "org.slf4j:slf4j-simple:${versions.slf4j}"
	
	    testImplementation "junit:junit:${versions.junit}"
	}
	
	configurations.all {
		exclude group: 'asm'
	}

	task createVersion {
	    doLast {
	        def version = project.version.toString()
	        def file = new File("$projectDir/src/main/resources/version.txt")
	        file.write(version)
	    }
	}
	
	classes {
	    dependsOn createVersion
	}
}
